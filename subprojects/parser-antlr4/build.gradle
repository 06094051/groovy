/*
 *  Licensed to the Apache Software Foundation (ASF) under one
 *  or more contributor license agreements.  See the NOTICE file
 *  distributed with this work for additional information
 *  regarding copyright ownership.  The ASF licenses this file
 *  to you under the Apache License, Version 2.0 (the
 *  "License"); you may not use this file except in compliance
 *  with the License.  You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing,
 *  software distributed under the License is distributed on an
 *  "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 *  KIND, either express or implied.  See the License for the
 *  specific language governing permissions and limitations
 *  under the License.
 */

apply plugin: 'me.champeau.gradle.antlr4'

def srcBase = "subprojects/parser-antlr4/src"
def srcMain = "$srcBase/main"
def srcTest = "$srcBase/test"
def antlr4Source = "$srcMain/antlr4"
def antlr4OutputRootDir = "$buildDir/generated-sources"
def antlr4Output = "$antlr4OutputRootDir/org/apache/groovy/parser/antlr4"

// set package for antlr generated classes
antlr4 {
    source = file(antlr4Source)
    extraArgs = ["-no-listener", "-package", "org.apache.groovy.parser.antlr4"]
    output = file(antlr4Output)
}

// make the Java compile task depend on the antlr4 task
compileJava.dependsOn antlr4

// add antlr4 to classpath
configurations {
   compile.extendsFrom antlr4
}

dependencies {
    antlr4 "com.tunnelvisionlabs:antlr4:$antlr4Version"
    testCompile project(':groovy-test')
}


task cleanGeneratedSources(type: Delete) {
    delete antlr4OutputRootDir
    file(antlr4OutputRootDir).mkdirs()
}

antlr4.dependsOn cleanGeneratedSources

// add the generated source files to the list of java sources
sourceSets.main.java.srcDirs += file("$antlr4OutputRootDir");
sourceSets.main.java.srcDirs += file("$srcMain/java");
sourceSets.main.groovy.srcDirs += file("$srcMain/groovy");
sourceSets.main.resources.srcDirs += file("$antlr4Source");
sourceSets.main.resources.srcDirs += file("$srcMain/resources");
sourceSets.test.java.srcDirs += file("$srcTest/java");
sourceSets.test.groovy.srcDirs += file("$srcTest/groovy");
sourceSets.test.resources.srcDirs += file("$srcTest/resources");

test {
    jvmArgs "-Dgroovy.extract.doc.comment=true", "-Dgroovy.antlr4.cache.threshold=100"
}
